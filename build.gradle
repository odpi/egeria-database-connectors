/*
 * SPDX-License-Identifier: Apache-2.0
 * Copyright Contributors to the ODPi Egeria project.
 */


/*
 * Used for Build scripts/plugins only
*/

buildscript {
    repositories { 
        jcenter()
        mavenCentral()
        // snapshots of egeria core ie -SNAPSHOT
        maven { url("https://odpi.jfrog.io/odpi/egeria-snapshot") }
    }

    dependencies {
        classpath 'com.netflix.nebula:gradle-aggregate-javadocs-plugin:3.0.1'
    } 
}

/*
 * Plugins for this parent module only - so just high level project related
 */

plugins {
        //id "nebula.lint" version "16.9.0"
        //id "nebula-aggregate-javadocs"
    }

/* run './gradlew aggregateJavadocs' at top level to build all docs & output to build/docs/javadoc */
apply plugin: 'nebula-aggregate-javadocs'


/*
 * Configuration for all projects - INCLUDING this one
 */

allprojects {

    group = 'org.odpi.egeria'
    version = '2.5-SNAPSHOT'

    repositories {
         jcenter()
         mavenCentral()
         // snapshots of egeria core ie -SNAPSHOT
         maven { url("https://odpi.jfrog.io/odpi/egeria-snapshot") }
    }


    // This enforces version checking but is slow to process
    //apply plugin: 'nebula.lint'
    //gradleLint {
    //    rules = ['all-dependency'] // TODO: Change to criticalRules when ready to enforce
    //
    //}
}

/*
 * Configuration for sub projects only
 */

subprojects {

    // Mostly java, so default to this for now
    apply plugin: 'java'

    // As we've migrated from maven - we'll assume all submodules publish directly to maven
    apply plugin: 'maven-publish'

    // Dependency Management - to fix versions. Pick up maven build settings for now
    dependencies {
        constraints {
            implementation("org.slf4j:slf4j-api:1.7.30")
            implementation 'org.odpi.egeria:data-manager-client:2.5-SNAPSHOT'
            implementation 'org.odpi.egeria:data-manager-api:2.5-SNAPSHOT'
            implementation 'org.odpi.egeria:database-integrator-api:2.5-SNAPSHOT'
            implementation 'org.odpi.egeria:open-connector-framework:2.5-SNAPSHOT'
            implementation 'org.slf4j:slf4j-api'
        }
    }

    // sources required to publish a legitimate maven package for distribution
    java {
        withSourcesJar()
    }

    // publish maven package
    publishing {
        publications {
            maven(MavenPublication) {
                from(components.java)
            }
        }
    }

    // Java language settings
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        sourceCompatibility = "1.8"
        targetCompatibility = "1.8"
    }
}
